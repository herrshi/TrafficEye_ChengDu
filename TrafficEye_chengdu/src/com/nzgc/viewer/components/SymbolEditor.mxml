<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 width="100%" height="100%"
		 minWidth="50"
		 minHeight="50"
		 xmlns:supportClasses="com.esri.ags.skins.supportClasses.*"
		 xmlns:components="com.nzgc.viewer.components.*"
		 >
	
	<fx:Script>
		<![CDATA[
			import com.esri.ags.symbols.SimpleFillSymbol;
			import com.esri.ags.symbols.SimpleLineSymbol;
			import com.esri.ags.symbols.SimpleMarkerSymbol;
			import com.esri.ags.symbols.Symbol;
			import com.esri.ags.symbols.TextSymbol;
			import com.nzgc.viewer.utils.SymbolUtil;
			
			import mx.core.IVisualElement;
			import mx.events.CloseEvent;
			import mx.events.ColorPickerEvent;
			import mx.events.FlexEvent;
			import mx.utils.ObjectUtil;
			
			import spark.events.IndexChangeEvent;
			
			[Bindable]
			private var _symbol:Symbol;
			
			[Bindable]
			private var _newSymbol:Symbol;
			
			private var _symbolUpdated:Boolean = false;
			

			/* --------------------------------------------------------------------
			Component proeprties
			-------------------------------------------------------------------- */

			/**
			 * ESRI Symbol to use as a model/starting point.
			 */
			public function set symbol(value:Symbol):void
			{
				_symbol = value;
				
				if (value)
				{
					_newSymbol = SymbolUtil.DuplicateSymbol(value);
				}
				
				updateCurrentState();
			}
			
			public function get symbol():Symbol
			{
				return _symbol;
			}
			
			/**
			 * ESRI Symbol generated by the component based on the current settings.
			 */
			public function get NewSymbol():Symbol
			{
				return _newSymbol;
			}
			
			
			/* --------------------------------------------------------------------
			Component UI functions
			-------------------------------------------------------------------- */
			
			private function updateCurrentState():void
			{
				if (_newSymbol is TextSymbol)
				{
					var ts:TextSymbol = _newSymbol as TextSymbol;
					
					alphaSlider.value = ts.alpha;
					pkrColour.selectedColor = ts.color;
					
					var fp:Object = {};
					if (ts.textFormat)
					{
						fp.fontFamily = ts.textFormat.font || "Arial";
						fp.fontWeight = ts.textFormat.bold ? "bold" : "normal";
						fp.fontStyle = ts.textFormat.italic ? "italic" : "normal";
						fp.fontSize = ts.textFormat.size || 14;
						fp.textDecoration = ts.textFormat.underline ? "underline" : "none";
					}
					else
					{
						fp.fontFamily = "Arial";
						fp.fontWeight = "normal";
						fp.fontStyle = "normal";
						fp.fontSize = 14;
						fp.textDecoration = "none";
					}
					
					fontPicker.fontProps = fp;
					
					currentState = "TextSymbol";
				}
				
				if (_newSymbol is SimpleFillSymbol)
				{
					// Update the control settings
					var sfs:SimpleFillSymbol = _newSymbol as SimpleFillSymbol;
					
					alphaSlider.value = sfs.alpha;
					pkrColour.selectedColor = sfs.color;
					fillDropDownList.selectedIndex = getStyleIndex(fillDropDownList.dataProvider as ArrayCollection,sfs.style);
					
					if (sfs.outline)
					{
						outlineAlphaSlider.value = sfs.outline.alpha;
						pkrOutlineColour.selectedColor = sfs.outline.color;
						outlineSizeStepper.value = sfs.outline.width;
						outlineDropDownList.selectedIndex = getStyleIndex(outlineDropDownList.dataProvider as ArrayCollection,sfs.outline.style);
					}
					else
					{
						outlineAlphaSlider.value = 1;
						pkrOutlineColour.selectedColor = sfs.color;
						outlineSizeStepper.value = 1;
						outlineDropDownList.selectedIndex = 0;
					}
					
					// Update the component state
					currentState = "SimpleFillSymbol";
				}
				
				if (_newSymbol is SimpleLineSymbol)
				{
					// Update the control settings
					var sls:SimpleLineSymbol = _newSymbol as SimpleLineSymbol;
					
					alphaSlider.value = sls.alpha;
					pkrColour.selectedColor = sls.color;
					lineDropDownList.selectedIndex = getStyleIndex(lineDropDownList.dataProvider as ArrayCollection,sls.style);
					lineSizeStepper.width = sls.width;
					
					// Update the component state
					currentState = "SimpleLineSymbol";
				}
				
				if (_newSymbol is SimpleMarkerSymbol)
				{
					// Update the control settings
					var sms:SimpleMarkerSymbol = _newSymbol as SimpleMarkerSymbol;
					
					markerSizeStepper.value = sms.size;
					alphaSlider.value = sms.alpha;
					pkrColour.selectedColor = sms.color;
					markerDropDownList.selectedIndex = getStyleIndex(markerDropDownList.dataProvider as ArrayCollection,sms.style);
					
					if (sms.outline)
					{
						outlineAlphaSlider.value = sms.outline.alpha;
						pkrOutlineColour.selectedColor = sms.outline.color;
						outlineSizeStepper.value = sms.outline.width;
						outlineDropDownList.selectedIndex = getStyleIndex(outlineDropDownList.dataProvider as ArrayCollection,sms.outline.style);
					}
					else
					{
						outlineAlphaSlider.value = 1;
						pkrOutlineColour.selectedColor = sms.color;
						outlineSizeStepper.value = 1;
						outlineDropDownList.selectedIndex = 0;
					}
					
					// Update the component state
					currentState = "SimpleMarkerSymbol";
				}
				
				updatePreview();
			}
			
			// Called when the selected colour on one of the colour pickers is updated
			protected function colourPicker_changeHandler(event:ColorPickerEvent):void
			{
				updatePreview();
			}
			
			// Called when the selected item in one of the dropdown lists is updated
			protected function dropDownList_changeHandler(event:IndexChangeEvent):void
			{
				updatePreview();
			}
			
			// Called when the value on one of the sliders or steppers is updated
			protected function control_changeHandler(event:Event):void
			{
				updatePreview();
			}

			// Called when the fontpicker properties change
			protected function fontPicker_fontSettingUpdateHandler(event:FlexEvent):void
			{
				updatePreview();
			}

			// Updates the swatch preview 
			private function updatePreview():void
			{
				previewGroup.removeAllElements();

				try
				{
					if (_newSymbol)
					{
						if (_newSymbol is SimpleMarkerSymbol)
						{
							var sms:SimpleMarkerSymbol = _newSymbol as SimpleMarkerSymbol;
							sms.alpha = alphaSlider.value;
							sms.size = markerSizeStepper.value;
							sms.color = pkrColour.selectedColor;
							sms.style = markerDropDownList.selectedItem.value;
							sms.xoffset = markerXOffsetStepper.value;
							sms.yoffset = markerYOffsetStepper.value;
							sms.angle = markerRotationStepper.value;
							
							if (outlineSizeStepper.value > 0)
							{
								sms.outline = new SimpleLineSymbol(outlineDropDownList.selectedItem.value,pkrOutlineColour.selectedColor,
									outlineAlphaSlider.value,outlineSizeStepper.value);
							}
							else
							{
								sms.outline = null;
							}
						}
						
						if (_newSymbol is SimpleLineSymbol)
						{
							var sls:SimpleLineSymbol = _newSymbol as SimpleLineSymbol;
							sls.alpha = alphaSlider.value;
							sls.width = lineSizeStepper.value;
							sls.color = pkrColour.selectedColor;
							sls.style = lineDropDownList.selectedItem.value;
						}
						
						if (_newSymbol is SimpleFillSymbol)
						{
							var sfs:SimpleFillSymbol = _newSymbol as SimpleFillSymbol;
							sfs.alpha = alphaSlider.value;
							sfs.color = pkrColour.selectedColor;
							sfs.style = fillDropDownList.selectedItem.value;
							
							if (outlineSizeStepper.value > 0)
							{
								sfs.outline = new SimpleLineSymbol(outlineDropDownList.selectedItem.value,pkrOutlineColour.selectedColor,
									outlineAlphaSlider.value,outlineSizeStepper.value);
							}
							else
							{
								sfs.outline = null;
							}
						}
						
						if (_newSymbol is TextSymbol)
						{
							// Get the font properties
							var fp:Object = fontPicker.fontProps;
							if (fp)
							{
								var txt:TextSymbol = _newSymbol as TextSymbol;
								
								txt.alpha = alphaSlider.value;
								txt.text = "Text";
								txt.color = pkrColour.selectedColor;
								txt.angle = markerRotationStepper.value;
								txt.xoffset = markerXOffsetStepper.value;
								txt.yoffset = markerYOffsetStepper.value;
								
								var txtFormat:TextFormat = new TextFormat(fp.fontFamily,fp.fontSize,pkrColour.value as uint);
								txtFormat.bold = fp.fontWeight == "bold";
								txtFormat.italic = fp.fontStyle == "italic";
								txtFormat.underline = fp.textDecoration == "underline";
								txt.textFormat = txtFormat;

								// Update the preview for the label
								var lab:Label = new Label();
								lab.text = "Text";
								lab.alpha = alphaSlider.value;
								lab.rotation = markerRotationStepper.value;
								lab.x = (previewGroup.width/2) - (lab.width/2) + markerXOffsetStepper.value;
								lab.y = (previewGroup.height/2) - (lab.height/2) + markerYOffsetStepper.value;
								
								lab.setStyle("fontFamily",fp.fontFamily);
								lab.setStyle("fontSize",fp.fontSize);
								lab.setStyle("fontStyle",fp.fontStyle);
								lab.setStyle("fontWeight",fp.fontWeight);
								lab.setStyle("textDecoration",fp.textDecoration);
								lab.setStyle("color",pkrColour.selectedColor);
								
								previewGroup.addElement(lab);
							}
						}
						else
						{
							var swatch:IVisualElement = IVisualElement(Symbol(_newSymbol).createSwatch(60, 40));
							previewGroup.addElement(swatch);
						}
					}
				}
				catch (error:Error)
				{
					// Do nothing
				}
			}
			
			/**
			 * Function to get the item index of the given style name from a style dropdown dataprovider.  
			 */			
			private function getStyleIndex(array:ArrayCollection, style:String):int
			{
				var index:int = -1;
				for (var i:int = 0; i < array.length; i++)
				{
					if (array.getItemAt(i).value == style)
					{
						index = i;
						break;
					}
				}
				return index;
			}
			
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="SimpleMarkerSymbol" />
		<s:State name="SimpleLineSymbol" />
		<s:State name="SimpleFillSymbol" />
		<s:State name="TextSymbol" />
	</s:states>
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<s:VGroup id="symbolSettings" width="100%"  height="100%" gap="5">
		
		<s:VGroup id="mainSymbol" width="100%" gap="5" paddingTop="5" >
			
			<s:Label id="labSettings" text="标志:" styleName="WidgetTitle" />
			
			<s:HGroup gap="5" verticalAlign="middle" height="25" width="100%" >
				<s:Label text="类型:" />
				
				<s:DropDownList id="markerDropDownList" toolTip="点击改变标记类型" width="100%"
								labelField="label" change="dropDownList_changeHandler(event)" selectedIndex="0"
								visible="false" visible.SimpleMarkerSymbol="true" 
								includeInLayout="false" includeInLayout.SimpleMarkerSymbol="true" >
					<mx:ArrayCollection>
						<fx:Object label="圆形标记" value="circle" />
						<fx:Object label="十字标记" value="cross" />
						<fx:Object label="菱形标记" value="diamond" />
						<fx:Object label="矩形标记" value="square" />
						<fx:Object label="三角标记" value="triangle" />
						<fx:Object label="×形标记" value="x" />
					</mx:ArrayCollection>
				</s:DropDownList>
				
				<s:DropDownList id="lineDropDownList" toolTip="点击改变绘线类型" width="100%"
								labelField="label" change="dropDownList_changeHandler(event)" selectedIndex="0"
								visible="false" visible.SimpleLineSymbol="true" 
								includeInLayout="false" includeInLayout.SimpleLineSymbol="true">
					<mx:ArrayCollection>
						<fx:Object label="实体线" value="solid" />
						<fx:Object label="短划虚线" value="dash" />
						<fx:Object label="点划虚线" value="dashdot" />
						<fx:Object label="双点划虚线" value="dashdotdot" />
						<fx:Object label="点虚线" value="dot" />
					</mx:ArrayCollection>
				</s:DropDownList>
				
				<s:DropDownList id="fillDropDownList" toolTip="点击改变填充类型" width="100%"
								labelField="label" change="dropDownList_changeHandler(event)" selectedIndex="0"
								visible="false" visible.SimpleFillSymbol="true" 
								includeInLayout="false" includeInLayout.SimpleFillSymbol="true">
					<mx:ArrayCollection>
						<fx:Object label="纯填充色" value="solid" />
						<fx:Object label="井状网格" value="cross" />
						<fx:Object label="×状网格" value="diagonalcross" />
						<fx:Object label="横线网格" value="horizontal" />
						<fx:Object label="竖线网格" value="vertical" />
						<fx:Object label="斜线网格" value="backwarddiagonal" />
						<fx:Object label="反斜线网格" value="forwarddiagonal" />
					</mx:ArrayCollection>
				</s:DropDownList>
				
				<s:Label text="颜色:" />
				<mx:ColorPicker id="pkrColour" toolTip="点击改变符号的颜色" 
								change="colourPicker_changeHandler(event)"/>
				
				<s:Label text="大小:" visible="false" 
						 visible.SimpleMarkerSymbol="true" includeInLayout="false" 
						 includeInLayout.SimpleMarkerSymbol="true" />
				<s:NumericStepper id="markerSizeStepper" minimum="5" maximum="30" 
								  change="control_changeHandler(event)" stepSize="1" 
								  visible="false" visible.SimpleMarkerSymbol="true" 
								  includeInLayout="false" includeInLayout.SimpleMarkerSymbol="true"/>
				
				<s:Label text="宽度:" visible="false" 
						 visible.SimpleLineSymbol="true" includeInLayout="false" 
						 includeInLayout.SimpleLineSymbol="true"/>
				<s:NumericStepper id="lineSizeStepper" minimum="1" maximum="10" 
								  change="control_changeHandler(event)" stepSize="1" 
								  visible="false" visible.SimpleLineSymbol="true" 
								  includeInLayout="false" includeInLayout.SimpleLineSymbol="true" />
			</s:HGroup>
			
			<components:FontPicker id="fontPicker" width="100%" visible="false" visible.TextSymbol="true" 
								   includeInLayout="false" includeInLayout.TextSymbol="true" showPreview="false" 
								   fontSettingUpdate="fontPicker_fontSettingUpdateHandler(event)"/>
			
			<s:HGroup gap="5" verticalAlign="middle" height="25" width="100%" >
				<s:Label text="透明度:" />
				<s:HSlider id="alphaSlider" minimum="0" maximum="1" width="100%" useHandCursor="true" 
						   change="control_changeHandler(event)" stepSize="0.1" />
			</s:HGroup>
			
			<s:HGroup gap="5" verticalAlign="middle" height="25" width="100%" 
					  visible="false" visible.SimpleMarkerSymbol="true" visible.TextSymbol="true"
					  includeInLayout="false" includeInLayout.SimpleMarkerSymbol="true" 
					  includeInLayout.TextSymbol="true" >
				
				<s:Label text="X轴 偏移量:" />
				<s:NumericStepper id="markerXOffsetStepper" minimum="-50" maximum="50" stepSize="1" 
								  toolTip="用于改变标记横向偏移量." 
								  change="control_changeHandler(event)" />
				
				<s:Label text="Y轴 偏移量:" />
				<s:NumericStepper id="markerYOffsetStepper" minimum="-50" maximum="50" stepSize="1" 
								  toolTip="用于改变标记垂直偏移量." 
								  change="control_changeHandler(event)" />
				
				<s:Label text="旋转:" />
				<s:NumericStepper id="markerRotationStepper" minimum="0" maximum="359" stepSize="1" 
								  toolTip="用于改变标记旋转方向." 
								  change="control_changeHandler(event)" />
			</s:HGroup>
			
		</s:VGroup>
		
		<s:VGroup id="outlineArea" width="100%" gap="5" visible="false" 
				  visible.SimpleFillSymbol="true" visible.SimpleMarkerSymbol="true" 
				  includeInLayout="false" includeInLayout.SimpleFillSymbol="true" 
				  includeInLayout.SimpleMarkerSymbol="true" >
			
			<s:Label id="labOutlineSettings" text="边框:" styleName="WidgetTitle" />
			
			<s:HGroup gap="5" verticalAlign="middle" height="25" width="100%">
				<s:Label text="类型:" />
				<s:DropDownList id="outlineDropDownList" toolTip="点击改变边框类型" width="100%"
								labelField="label" change="dropDownList_changeHandler(event)" selectedIndex="0">
					<mx:ArrayCollection>
						<fx:Object label="实体线" value="solid" />
						<fx:Object label="短划虚线" value="dash" />
						<fx:Object label="点划虚线" value="dashdot" />
						<fx:Object label="双点虚划线" value="dashdotdot" />
						<fx:Object label="点虚线" value="dot" />
						<fx:Object label="无边框" value="none" />
					</mx:ArrayCollection>
				</s:DropDownList>
				
				<s:Label text="颜色:" />
				<mx:ColorPicker id="pkrOutlineColour" toolTip="点击改变标记图形边线颜色." 
								change="colourPicker_changeHandler(event)"/>
				
				<s:Label text="宽度:" />
				<s:NumericStepper id="outlineSizeStepper" minimum="0" maximum="10" 
								  change="control_changeHandler(event)" stepSize="1" />
				
			</s:HGroup>
			
			<s:HGroup gap="5" verticalAlign="middle" height="25" width="100%">
				<s:Label text="透明度:" />
				<s:HSlider id="outlineAlphaSlider" minimum="0" maximum="1" width="100%" useHandCursor="true" 
						   change="control_changeHandler(event)" stepSize="0.1" />
			</s:HGroup>
			
		</s:VGroup>
		
		<s:HGroup id="previewGroup" width="100%" height="50" horizontalAlign="center" 
				  verticalAlign="middle" paddingTop="5" paddingBottom="5">
		</s:HGroup>
		
	</s:VGroup>
	
</s:Group>